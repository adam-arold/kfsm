import org.gradle.internal.os.OperatingSystem

plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.3.41'
    id 'org.jetbrains.dokka' version '0.9.18'
    id 'maven-publish'
}
repositories {
    jcenter()
    mavenCentral()
}

group = 'io.jumpco.open'
version = '0.1.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

def useMingw = defaultProfile.contains('mingw')
def useLinux = defaultProfile.contains('linux')
def useMacos = defaultProfile.contains('macos')
def useJs = defaultProfile.contains('js')
def useJvm = defaultProfile.contains('jvm')
def useWasm = defaultProfile.contains('wasm')

def profile = ext.find('profile') ?: defaultProfile
if (profile) {
    if (profile.contains('default')) {
        if (OperatingSystem.current().isWindows()) {
            useMingw = true
            logger.lifecycle "Detected ${OperatingSystem.current()} using mingw"
        }
        if (OperatingSystem.current().isLinux()) {
            useLinux = true
            logger.lifecycle "Detected ${OperatingSystem.current()} using linux"
        }
        if (OperatingSystem.current().isMacOsX()) {
            useMacos = true
            logger.lifecycle "Detected ${OperatingSystem.current()} using macos"
        }
    }
    if (profile.contains('linux')) {
        useLinux = true
    }
    if (profile.contains('mingw')) {
        useMingw = true
    }
    if (profile.contains('macos')) {
        useMacos = true
    }
    if (profile.contains('wasm')) {
        useWasm = true
    }
}

kotlin {
    if (useJvm) {
        jvm() {
            mavenPublication {
                artifactId = 'kfsm-jvm'
            }
        }
    }
    if (useJs) {
        js {
            browser {
            }
            nodejs {
            }
            mavenPublication {
                artifactId = 'kfsm-js'
            }
        }
    }
    if (useMingw) {
        mingwX64('mingw') {
            mavenPublication {
                artifactId = 'kfsm-mingwX64'
            }
        }
    }
    if (useLinux) {
        linuxX64('linux') {
            mavenPublication {
                artifactId = 'kfsm-linuxX64'
            }
        }
    }
    if (useMacos) {
        macosX64('macos') {
        }
    }
    if (useWasm) {
        wasm32("wasm") {
            mavenPublication {
                artifactId = 'kfsm-wasm32'
            }
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
                implementation 'io.mockk:mockk-common:1.9.3'
            }
        }
        if (useJvm) {
            jvmMain {
                dependencies {
                    implementation kotlin('stdlib-jdk8')
                }
            }
            jvmTest {
                dependencies {
                    implementation kotlin('test')
                    implementation kotlin('test-junit')
                    implementation 'io.mockk:mockk:1.9.3'
                }
            }
        }
        if (useJs) {
            jsMain {
                dependencies {
                    implementation kotlin('stdlib-js')
                }
            }
            jsTest {
                dependencies {
                    implementation kotlin('test-js')
                }
            }
        }
        if (useMingw) {
            configureNative(mingwMain, mingwTest)
        }
        if (useLinux) {
            configureNative(linuxMain, linuxTest)
        }
        if (useMacos) {
            configureNative(macosMain, macosTest)
        }
        if (useWasm) {
            configureNative(wasmMain, wasmTest)
        }
    }
}

def configureNative(srcSetMain, srcSetTest) {
    srcSetMain.kotlin.srcDirs = ['src/native/main']
    srcSetTest.kotlin.srcDirs = ['src/native/test']
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
    includeNonPublic = false
    impliedPlatforms = ["Common"] // This will force platform tags for all non-common sources e.g. "JVM"
    kotlinTasks {
        // dokka fails to retrieve sources from MPP-tasks so they must be set empty to avoid exception
        // use sourceRoot instead (see below)
        []
    }
    sourceRoot {
        // assuming there is only a single source dir...
        path = kotlin.sourceSets.commonMain.kotlin.srcDirs[0]
        platforms = ['Common']
    }
    if (useJvm) {
        sourceRoot {
            // assuming there is only a single source dir...
            path = kotlin.sourceSets.jvmMain.kotlin.srcDirs[0]
            platforms << 'JVM'
        }
    }
    if (useJs) {
        sourceRoot {
            // assuming there is only a single source dir...
            path = kotlin.sourceSets.jsMain.kotlin.srcDirs[0]
            platforms << 'JS'
        }
    }
    if (useLinux) {
        sourceRoot {
            // assuming there is only a single source dir...
            path = kotlin.sourceSets.linuxMain.kotlin.srcDirs[0]
            platforms << 'Linux'
        }
    }
    if (useMacos) {
        sourceRoot {
            // assuming there is only a single source dir...
            path = kotlin.sourceSets.macosMain.kotlin.srcDirs[0]
            platforms << 'macOS'
        }
    }
    samples = ['src/commonTest/kotlin/io/jumpco/open/kfsm/turnstile.kt']
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from files('src/commonMain/kotlin')
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from dokka.outputDirectory

}

gradle.afterProject {
    tasks.forEach { task ->
        if (task.name.endsWith('Classes')) {
            sourcesJar.dependsOn(task)
        }
        if (task.name.contains('dokka')) {
            javadocJar.dependsOn(task)
        }
        if (task.name.contains('Publication')) {
            publish.dependsOn(task)
        }
    }
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

publishing {
    publications {
        kfsm(MavenPublication) {
            artifact javadocJar
            artifact sourcesJar
        }
    }
    publications.all { publication ->
        logger.info "publication:$publication.name"
        if (publication.name == 'kotlinMultiplatform') {
            publication.artifactId = "kfsm-common"
        }
    }
}
